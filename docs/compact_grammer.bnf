(* From https://pubs.opengroup.org/onlinepubs/009695399/utilities/xcu_chap02.html#tag_02_10 *)

(* -------------------------------------------------------
   The grammar tokens
   ------------------------------------------------------- *)


%token  WORD
%token  ASSIGNMENT_WORD
%token  IO_NUMBER
(*  0 - stdin, 1 - stdout, 2 - stderr  *)

%token  QUOTE  DQUOTE
(*      '''     '"'  *)

%token  DLESS  DGREAT LESS GREAT
(*      '<<'   '>>'   '<'   '>'  *)

%token PIPE
(*     '|'   *)

(* Bonus *)

%token  AND_IF    OR_IF 
(*      '&&'      '||'  *)

%token LBRACKET RBRACKET
(*        '('      ')'   *)

(* -------------------------------------------------------
   The Grammar
   ------------------------------------------------------- *)


%start  complete_command
%%

(* Basic components *)
word             ::=   QUOTE  WORD QUOTE
                 |     DQUOTE WORD DQUOTE
                 |            WORD       
                 ;
(* Structural syntax *)
complete_command ::= complete_command AND_IF pipeline
                 |   complete_command OR_IF  pipeline
                 |                           pipeline
                 ;
pipe_line        ::= pipe_line PIPE command
                 |                  command
                 ;
command          ::= subshell [subshell_redirects]
                 |   simple_command
                 ;

(* Command syntax *)
simple_command   ::= cmd_prefix cmd_word [cmd_suffix]
                 |   cmd_prefix
                 |   cmd_name [cmd_suffix]
                 ;
cmd_name         ::= word
                 ;(* Apply rule 7a *)
cmd_word         ::= word
                 ; (* Apply rule 7b *)
cmd_prefix       ::= [cmd_prefix] (io_redirect | ASSIGNMENT_WORD)
                 ;
cmd_suffix       ::= [cmd_suffix] (io_redirect | cmd_parameter)
                 ;
cmd_parameter    ::= word
                 ;

(* Subshell syntax *)
subshell           ::= LBRACKET complete_command RBRACKET
                 ;
subshell_redirects ::= [subshell_redirects] io_redirect
                 ;

(* Redirection syntax *)
io_redirect      ::= [IO_NUMBER] (io_file | io_here_doc)
                 ;
io_file          ::= (LESS | GREAT | DGREAT) filename
                 ;
filename         ::= word
                 ; (* Apply rule 2 *)
io_here_doc      ::= DLESS   here_doc_end
				     ;
here_doc_end     ::= word
                 ; (* Apply rule 3 *)