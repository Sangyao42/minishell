(* From https://pubs.opengroup.org/onlinepubs/009695399/utilities/xcu_chap02.html#tag_02_10 *)

(* -------------------------------------------------------
   The grammar tokens
   ------------------------------------------------------- *)


%token  WORD
%token  ASSIGNMENT_WORD

%token  QUOTE  DQUOTE
(*      '''     '"'  *)

%token  DLESS  DGREAT LESS GREAT
(*      '<<'   '>>'   '<'   '>'  *)

%token PIPE
(*     '|'   *)

(* -------------------------------------------------------
   The Grammar
   ------------------------------------------------------- *)


%start  complete_command
%%

(* Basic components *)
word             ::=   QUOTE  WORD QUOTE
                 |     DQUOTE WORD DQUOTE
                 |            WORD       
                 ;
(* Structural syntax *)
complete_command ::= command PIPE complete_command
                 |   command
                 ;

(* Command syntax *)
command          ::= cmd_prefix cmd_word [cmd_suffix]
                 |   cmd_prefix
                 |              cmd_name [cmd_suffix]
                 ;
cmd_name         ::= word
                 ;(* Apply rule 7a *)
cmd_word         ::= word
                 ; (* Apply rule 7b *)
cmd_prefix       ::= (io_redirect | ASSIGNMENT_WORD) [cmd_prefix]
                 ;
cmd_suffix       ::= (io_redirect | cmd_parameter) [cmd_suffix] 
                 ;
cmd_parameter    ::= word
                 ;

(* Redirection syntax *)
io_redirect      ::= io_file | io_here_doc
                 ;
io_file          ::= (LESS | GREAT | DGREAT) filename
                 ;
filename         ::= word
                 ; (* Apply rule 2 *)
io_here_doc      ::= DLESS   here_doc_end
				     ;
here_doc_end     ::= word
                 ; (* Apply rule 3 *)